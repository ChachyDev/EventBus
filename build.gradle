plugins {
    id 'java'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.12.3'
}

group 'dev.deamsy'
version gitVersion()

if (version.endsWith('.dirty')) {
    version = version.substring(0, version.length() - '.dirty'.length())
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    implementation 'org.ow2.asm:asm-tree:8.0.1'
    implementation 'org.jetbrains:annotations:19.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

jar {
    manifest.attributes(
            'Implementation-Title': project.name,
            'Implementation-Vendor': 'deamsy',
            'Implementation-Version': project.version
    )
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/deamsy/EventBus")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}